import org.apache.tools.ant.filters.*

buildscript {
	ext {
		kotlinVersion = "1.3.0-rc-198"
		springBootVersion = "2.1.1.RELEASE"
        repository = "https://github.com/ArnauAregall/aregalio-tech-site-kotlin"
	}
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
		classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}")
		classpath("org.jetbrains.kotlin:kotlin-allopen:${kotlinVersion}")
	}
}

apply plugin: 'kotlin'
apply plugin: 'kotlin-spring'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

description = 'arnau.aregall.tech Kotlin + Spring Boot + Gradle Demo Website'
group = 'tech.aregall.arnau'
version = '1.0.0'
sourceCompatibility = 1.8

dependencies {
	compile('org.springframework.boot:spring-boot-starter-mustache')
	compile('org.springframework.boot:spring-boot-starter-web')
	compile('org.springframework.boot:spring-boot-devtools')
	compile('com.fasterxml.jackson.module:jackson-module-kotlin')
	compile("org.jetbrains.kotlin:kotlin-stdlib-jdk8")
	compile("org.jetbrains.kotlin:kotlin-reflect")
	testCompile('org.springframework.boot:spring-boot-starter-test')
}

bootJar {
	baseName = "arnau-aregall-tech-website"
}

compileKotlin {
	kotlinOptions {
		freeCompilerArgs = ["-Xjsr305=strict"]
		jvmTarget = "1.8"
	}
}
compileTestKotlin {
	kotlinOptions {
		freeCompilerArgs = ["-Xjsr305=strict"]
		jvmTarget = "1.8"
	}
}

repositories {
	mavenCentral()
}

idea {
    module {
        inheritOutputDirs = false
        outputDir = file("$buildDir/classes/kotlin/main")
    }
}

processResources() {
    filesMatching('**/*.properties') {
        it.filter(ReplaceTokens,
                tokens: [
                        description: project.description,
                        version: project.version,
                        repository: project.ext.repository,
                        gradleVersion: project.gradle.gradleVersion,
                        springBootVersion: project.ext.springBootVersion,
                        kotlinVersion: project.ext.kotlinVersion
                ])
    }
}
